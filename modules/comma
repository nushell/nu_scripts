def unindent [] {
    let txt = $in | lines | range 1..
    let indent = $txt.0 | parse --regex '^(?P<indent>\s*)' | get indent.0 | str length
    $txt
    | each {|s| $s | str substring $indent.. }
    | str join (char newline)
}

def 'path parents' [] {
    $in
    | path expand
    | path split
    | reduce -f [ '' ] {|x, acc| [( $acc.0 | path join $x ), ...$acc] }
    | range ..-2
}

def find [] {
    $in
    | path parents
    | filter {|x| $x | path join ',.nu' | path exists }
    | get 0?
}

def comma_file [] {
    [
        {
          condition: {|_, after| not ($after | path join ',.nu' | path exists)}
          code: "$env.comma = null"
        }
        {
          condition: {|_, after| $after | path join ',.nu' | path exists}
          code: "
          print $'(ansi default_underline)(ansi default_bold),(ansi reset).nu (ansi green_italic)detected(ansi reset)...'
          print $'(ansi yellow_italic)activating(ansi reset) (ansi default_underline)(ansi default_bold),(ansi reset) module with `(ansi default_dimmed)(ansi default_italic)source ,.nu(ansi reset)`'
          source ,.nu
          "
        }
    ]
}

export-env {
    $env.config = ( $env.config | upsert hooks.env_change.PWD { |config|
        let o = ($config | get -i hooks.env_change.PWD)
        let val = (comma_file)
        if $o == null {
            $val
        } else {
            $o | append $val
        }
    })
    let k = (gensym)
    $env.commax = {
        sub: $k.0
        dsc: $k.1
        act: $k.2
        cmp: $k.3
    }
}

def gensym [] {
    mut r = []
    let rk = random chars
    for i in 1..4 {
        let b = ($i - 1) * 5
        let e = $i * 5
        $r ++= [($rk | str substring $b..$e)]
    }
    $r
    # TODO: debug
    # [sub dsc act cmp]
}

def log [tag? -c] {
    let o = $in
    if ($c) {
        echo $'---(char newline)' | save -f ~/.cache/comma.log
    } else {
        echo $'---($tag)---($o | describe)(char newline)($o | to yaml)' | save -a ~/.cache/comma.log
    }
    $o
}

def first-type [type] {
    for i in $in {
        if ($i | describe -d).type == $type {
            return $i
        }
    }
}

def 'as act' [] {
    let o = $in
    let ix = $env.commax
    let t = ($o | describe -d).type
    if $t == 'closure' {
        { $ix.act: $o }
    } else if ($ix.sub in $o) {
        null
    } else if ($ix.act in $o) {
        $o
    } else {
        null
    }
}

def run [tbl] {
    let loc = $in
    let ix = $env.commax
    mut act = $tbl
    mut arg = []
    for i in $loc {
        let a = $act | as act
        if ($a | is-empty) {
            if ($ix.sub in $act) and ($i in ($act | get $ix.sub)) {
                let n = $act | get $ix.sub | get $i
                $act = $n
            } else if $i in $act {
                let n = $act | get $i
                $act = $n
            } else {
                $act = {|| print $"not found `($i)`"}
                break
            }
        } else {
            $arg ++= [$i]
        }
    }
    let a = $act | as act
    if ($a | is-empty) {
        let c = if $ix.sub in $act { $act | get $ix.sub | columns } else { $act | columns }
        print $'require argument: ($c)'
    } else {
        do ($a | get $ix.act) $arg
    }
}

def complete [tbl] {
    let argv = $in
    let ix = $env.commax
    mut tbl = $env.comma
    for i in $argv {
        let c = if ($i | is-empty) {
            $tbl
        } else {
            let tp =  ($tbl | describe -d).type
            if ($tp == 'record') and ($i in $tbl) {
                let j = $tbl | get $i
                if $ix.sub in $j {
                    $j | get $ix.sub
                } else {
                    $j
                }
            } else {
                $tbl
            }
        }
        let a = $c | as act
        if not ($a | is-empty) {
            let r = do ($a | get $ix.cmp) $argv
            $tbl = $r
        } else {
            $tbl = $c
        }
    }
    match ($tbl | describe -d).type {
        record => {
            $tbl
            | transpose k v
            | each {|x|
                if ($x.v | describe -d).type == 'closure' {
                    $x.k
                } else if $ix.dsc in $x.v {
                    { value: $x.k, description: ($x.v | get $ix.dsc) }
                } else {
                    $x.k
                }
            }
        }
        list => { $tbl }
        _ => { $tbl }
    }
}

def 'parse argv' [] {
    let context = $in
    $context.0
    | str substring 0..$context.1
    | split row -r '\s+'
    | range 1..
    | where not ($it | str starts-with '-')
}

def compos [...context] {
    $context
    | parse argv
    | complete $env.comma
}

export def , [...args:string@compos] {
    if ($args | is-empty) {
        if ([$env.PWD, ',.nu'] | path join | path exists) {
            ^$env.EDITOR ,.nu
        } else {
            let a = [yes no] | input list 'create ,.nu?'
            if $a == 'yes' {
                $"
                $env.commav = {

                }
                $env.comma = {
                    created: { '(date now | format date '%Y-%m-%d[%w]%H:%M:%S')' }
                    hello: {
                        $env.commax.act: {|x| print $'hello \($x\)' }
                        $env.commax.dsc: 'hello \(x\)'
                        $env.commax.cmp: {|args| $args}
                    }
                    open: {
                        $env.commax.sub: {
                            any: {
                                $env.commax.act: {|x| open $x.0}
                                $env.commax.cmp: {ls | get name}
                                $env.commax.dsc: 'open a yaml file'
                            }
                            json: {
                                $env.commax.act: {|x| open $x.0}
                                $env.commax.cmp: {ls *.json | get name}
                                $env.commax.dsc: 'open a json file'
                            }
                        }
                        $env.commax.dsc: 'open a file'
                    }
                }
                "
                | unindent
                | save $",.nu"
                #source ',.nu'
            }
        }
    } else {
        $args | run $env.comma
    }
}
